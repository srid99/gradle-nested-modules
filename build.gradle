group 'in.srid'
version '1.0-SNAPSHOT'

apply plugin: 'base'

defaultTasks 'clean', 'build', 'aggregateTestReport', 'jacocoTestReport'

subprojects {
    apply plugin: 'java'
    apply plugin: "jacoco"

    def isApp = project.buildFile.path.contains("/apps/$project.name")
    if (isApp) {
        apply plugin: 'application'
    }

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    task testJar(type: Jar) {
        classifier = 'test'
        from sourceSets.test.output
    }

    artifacts {
        testRuntime testJar
    }

    test {
        reports.html.enabled = false

        jacoco {
            destinationFile = file("${rootProject.buildDir}/jacoco/jacoco.exec")
        }
    }
}

task aggregateTestReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/tests/test")
    reportOn subprojects*.test
}
